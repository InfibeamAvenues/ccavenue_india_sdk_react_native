buildscript {
  ext.getExtOrDefault = {name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['CcavenueIndiaSdkReactNative_' + name]
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.7.2"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}


apply plugin: "com.android.library"
apply plugin: "kotlin-android"

apply plugin: "com.facebook.react"

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["CcavenueIndiaSdkReactNative_" + name]).toInteger()
}

android {
  namespace "com.ccavenueindiasdkreactnative"

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  buildFeatures {
    buildConfig true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main {
      java.srcDirs += [
        "generated/java",
        "generated/jni"
      ]
    }
  }
}


def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withInputStream { instr ->
        localProperties.load(instr)
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url = uri("https://jitpack.io") }

        // CCAvenue SDK 2.0
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/InfibeamAvenues/CCAvenue_SDK_2.0_UAT")
            credentials {
                username = localProperties.getProperty("gpr.usr") ?: "InfibeamAvenues"
                password = localProperties.getProperty("gpr.key") ?: ""
            }
        }

        // CCAvenue Tappay SDK
        maven {
            name = "GitHubPackagesTappay"
            url = uri("https://maven.pkg.github.com/uvik-technology/CCAvenue_tappay_sdk")
            credentials {
                username = localProperties.getProperty("sdk.username") ?: ""
                password = localProperties.getProperty("sdk.pwd") ?: ""
            }
        }

        // DexGuard Repository
        maven {
            url = uri("https://maven.guardsquare.com")
            credentials {
                username = localProperties.getProperty("dexguardMavenUser") ?: ""
                password = localProperties.getProperty("dexguardMavenPassword") ?: ""
            }
            content {
                includeGroupByRegex("com\\.guardsquare.*")
            }
            authentication {
                // 'create' is for Kotlin DSL; in Groovy use 'basic'
                basic(BasicAuthentication)
            }
        }
    }
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  implementation "com.facebook.react:react-android"
  implementation "com.ccavenue.indiasdk:indiasdk-uat:0.0.21"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
